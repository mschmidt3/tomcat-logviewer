buildscript {
    repositories {
        mavenLocal()
        maven { url "${repo}" }
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:4.8.0"
    }
}

version="0.9.13"

repositories {
    mavenLocal()
    maven { url "${repo}" }
}

apply plugin:"war"
apply plugin:'nebula.rpm'

configurations {
    binaries
}

dependencies {
    binaries group: 'org.codehaus.groovy', name: 'groovy-all', version: "${groovyVersion}"
}

war {

    from ("src/main/web-app/") {
        include 'index.html', 'showlog.groovy'
        into '/'
    }

    from ("src/main/web-app/") {
        include 'about.html'
        into '/'
        expand appversion: "${version}"
    }


    from ("src/main/web-app/js/") {
        include 'logviewer.js'
        into '/js/'
    }
    from ("src/main/web-app/css/") {
        include 'logviewer.css'
        into '/css/'
    }
    from ("src/main/web-app/WEB-INF") {
        include 'web.xml'
        into '/WEB-INF/'
    }
    from (configurations.binaries){
        into '/WEB-INF/lib/'
    }

    archiveName = "logviewer.war"
}


task rpm(type:Rpm){
    description "Create logviewer rpm file"
    // def basedir = "/usr/share/tomcat/webapps/"
    def basedir = "/opt/webapps/tomcat-logviewer"
    def rpmPackager = System.getenv('JOB_NAME') ? "Jenkins: ${System.getenv('JOB_NAME')}" :  "gradle (local)"
    packageName "tomcat-logviewer"
    release "${rpmRelease}"

    user            "tomcat"
    permissionGroup "tomcat"
    vendor          ""
    provides        "logviewer"
    arch            "NOARCH"
    type            "BINARY"
    os              "LINUX"
    packageGroup    "Applications/Tomcat"
    license         "ASL 2.0"
    packager        "${rpmPackager}"
    summary         "tomcat log viewer in groovy"
    packageDescription """A Tomcat logviewer written in groovy.
There is no source repository. All functionality is in readable form in the war file.
Link file ${basedir}/logviewer.war to your tomcat webapps directory.
"""
    requires ('tomcat', '6.0', GREATER )

    from( "${buildDir}/libs/logviewer.war" ){
        into "${basedir}/"
        addParentDirs false
        createDirectoryEntry true
    }

}
rpm.dependsOn war
